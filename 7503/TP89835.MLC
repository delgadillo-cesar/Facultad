* REGISTROS
* 4: TOP STACK
* 5: TOP HEAP
* 7: INVOCACION A RUTINA
PRUEBA   SUBENTRY BASES=(3)
         LA    4,STACK
         LA    5,HEAP
         ST    5,AUX
         LA    8,9
         BAL   7,INIVECT
         BAL   7,DUMMY
         BAL   7,PROCESAR
         BAL   7,MOSTRAR
         SUBEXIT


* REGISTROS
* 4: TOP STACK
* 6: ELEMENTO STACK
* 7: RETORNO
PUSH     ST    6,0(4)
         A     4,=F'4'
         BR    7


* REGISTROS
* 4: TOP STACK
* 6: ELEMENTO STACK
* 7: RETORNO
POP      S     4,=F'4'
         L     6,0(4)
         BR    7


* REGISTROS
* 5: TOP HEAP
* 6: IN--> BYTES A RESERVAR
*   OUT --> DIRECCION RESERVADA
* 7: RETORNO
MALLOC   ST    5,AUX
         AR    5,6
         L     6,AUX
         BR    7         

         
* REGISTROS: 
* 7: DIRECCION DE RETORNO
* 8: CANTIDAD DE ESTACIONES
* 9: VECTOR DE ESTACIONES
INIVECT  LR    6,7
         BAL   7,PUSH
         LA    9,VECEST
OTRAEST  MVI   0(9),C'N'
         MVC   1(4,9),=F'-1'
         A     9,=F'5'
         BCT   8,OTRAEST
         BAL   7,POP
         LR    7,6
         BR    7


* REGISTROS: 
* 7: DIRECCION DE RETORNO
* 8: ESTACION 1
* 9: ESTACION 2
* 10: AUX
* 11: AUX
LOADTRAM LR    6,7
         BAL   7,PUSH
         S     8,=F'1'
         LA    10,0
         LR    11,8
         M     10,=F'5'
         LA    10,VECEST
         AR    10,11
         MVC   NODOVEC,0(10)
         CLC   DIRLIST,=F'-1'  LISTA VACIA
         BE    NEWNODE
         L     10,DIRLIST
LEERNODO MVC   NODLIST,0(10)
         CLC   DIRSIG,=F'-1'   ULTIMO NODO
         BE    NEWNODE
         L     10,DIRSIG
         B     LEERNODO
NEWNODE  L     6,=F'5'        CANTIDAD DE BYTES DEL NODO DE LA LISTA
         BAL   7,MALLOC
         ST    6,1(10)
         STC   9,0(6)
         MVC   1(4,6),=F'-1'
         BAL   7,POP
         LR    7,6
         BR    7
         BAL   7,POP
         LR    7,6
         BR    7



* REGISTROS
* 7: RETORNO
* 10: DIRECCION DEL NODO DE LINEA ACTUAL
NEWLINEA LR    6,7
         BAL   7,PUSH
         LA    6,8
         BAL   7,MALLOC
         CLC   LINEAS,=F'-1'
         BNE   NOFIRST
         ST    6,LINEAS
         B     GENNODO
NOFIRST  ST    6,4(10)
GENNODO  LR    10,6
         LA    6,21
         BAL   7,MALLOC
         ST    6,0(10)
         MVI   0(6),X'FF'
         MVC   4(4,10),=F'-1'
         BAL   7,POP
         LR    7,6
         BR    7


* REGISTROS
* 6: VALOR A AGREGAR
* 7: RETORNO
* 10: DIRECCION DEL NODO DE LINEA ACTUAL
* 11: POSICION DEL VECTOR
ADDTOLIN L     11,0(10)
BUSCAR   CLI   0(11),X'FF'
         BE    ULTIMO
         A     11,=F'1'
         B     BUSCAR
ULTIMO   STC   6,0(11)
         A     11,=F'1'
         MVI   0(11),X'FF'
         BR    7


* REGISTROS:
* 7: DIRECCION DE RETORNO
* 8: VECTOR ESTACIONES
* 11: AUX
* 12: NODO ACTUAL DE LA LISTA DE RELACIONES
PROCREL  LR    6,7
         BAL   7,PUSH
         CL    12,=F'-1'
         BE    OMITEREL
         LR    6,12
         BAL   7,PUSH
         MVC   NODLIST,0(12)
         LA    6,0
         IC    6,ESTACION
         BAL   7,PROCEST
         BAL   7,POP
         LR    12,6
         MVC   NODLIST,0(12)
         L     12,DIRSIG
         BAL   7,PROCREL
OMITEREL BAL   7,POP
         LR    7,6
         BR    7


* REGISTROS: 
* 6: ESTACION
* 7: DIRECCION DE RETORNO
* 8: VECTOR ESTACIONES
* 11: AUX
* 12: NODO ACTUAL DE LA LISTA DE RELACIONES
PROCEST  LR    11,6
         LR    6,7
         BAL   7,PUSH
         LR    7,11
         S     7,=F'1'
         LA    6,0
         M     6,=F'5'
         LA    8,VECEST
         AR    8,7
         MVC   NODOVEC,0(8)
         CLI   FLAG,C'N'
         BNE   OMITIRE
         MVI   FLAG,C'S'
         MVC   0(5,8),NODOVEC
         LR    6,11
         BAL   7,ADDTOLIN
         L     12,DIRLIST
         BAL   7,PROCREL
OMITIRE  BAL   7,POP
         LR    7,6
         BR    7


* REGISTROS
* 7: RETORNO
* 8: VECTOR ESTACIONES
* 9: CONTADOR DE REPETICION
* 10: DIRECCION DEL NODO DE LINEA ACTUAL
PROCESAR LR    6,7
         BAL   7,PUSH
         LA    8,VECEST
         LA    9,20
OTRO     MVC   NODOVEC,0(8)
         CLI   FLAG,C'N'
         BNE   OMITIR
         BAL   7,NEWLINEA
         LR    6,8
         BAL   7,PUSH
         LA    6,21
         SR    6,9
         BAL   7,PROCEST
         BAL   7,POP
         LR    8,6
OMITIR   A     8,=F'5'
         BCT   9,OTRO
         BAL   7,POP
         LR    7,6
         BR    7






DUMMY    LR    6,7
         BAL   7,PUSH
* (8,1)
         LA    8,1
         LA    9,8
         BAL   7,LOADTRAM
         LA    8,8
         LA    9,1
         BAL   7,LOADTRAM
* (3,6)
         LA    8,3
         LA    9,6
         BAL   7,LOADTRAM
         LA    8,6
         LA    9,3
         BAL   7,LOADTRAM
* (4,9)
         LA    8,4
         LA    9,9
         BAL   7,LOADTRAM
         LA    8,9
         LA    9,4
         BAL   7,LOADTRAM
* (1,7)
         LA    8,1
         LA    9,7
         BAL   7,LOADTRAM
         LA    8,7
         LA    9,1
         BAL   7,LOADTRAM
* (2,7)
         LA    8,2
         LA    9,7
         BAL   7,LOADTRAM
         LA    8,7
         LA    9,2
         BAL   7,LOADTRAM
* (1,4)
         LA    8,1
         LA    9,4
         BAL   7,LOADTRAM
         LA    8,4
         LA    9,1
         BAL   7,LOADTRAM
* (5,4)
         LA    8,5
         LA    9,4
         BAL   7,LOADTRAM
         LA    8,4
         LA    9,5
         BAL   7,LOADTRAM
* (1,9)
         LA    8,1
         LA    9,9
         BAL   7,LOADTRAM
         LA    8,9
         LA    9,1
         BAL   7,LOADTRAM
         BAL   7,POP
         LR    7,6
         BR    7

* REGISTROS:
* 7: DIRECCION DE RETORNO
* 9: PUNTERO AL ELEMENTO DE LA LISTA DE ESTACIONES
* 10: POSICION DEL STRING
* 11: AUX
TEXTEST  LR    6,7
         BAL   7,PUSH
         LA    11,0
         IC    11,0(9)
         CVD   11,DOBLE
         ZAP   EMPAQ,DOBLE
         UNPK  ZONEADO,EMPAQ
         MVZ   ZONEADO,=XL2'F0F0'
         MVC   0(2,10),ZONEADO
         MVC   2(2,10),=CL2', '
         A     10,=F'4'
         BAL   7,POP
         LR    7,6
         BR    7


* REGISTROS:
* 7: DIRECCION DE RETORNO
* 10: POSICION DEL STRING
IMPRIMIR LR    6,7
         BAL   7,PUSH
         WTO   MF=(E,WTOBLOCK)
         LA    10,TEXTO
         MVC   0(80,10),VACIO
         BAL   7,POP
         LR    7,6
         BR    7


* REGISTROS: 
* 7: DIRECCION DE RETORNO
* 8: PUNTERO AL NODO DE LINEA
* 9: PUNTERO AL ELEMENTO DE LA LISTA DE ESTACIONES
* 10: POSICION DEL STRING
MOSTRAR  LR    6,7
         BAL   7,PUSH
         LA    10,TEXTO

         L     8,LINEAS
OTRALINE CL    8,=F'-1'
         BE    NADAMOST
         MVC   0(7,10),=CL7'LINEA: '
         A     10,=F'7'
         L     9,0(8)
OTRAESTA CLI   0(9),X'FF'
         BE    FINLINEA
         BAL   7,TEXTEST
         A     9,=F'1'
         B     OTRAESTA
FINLINEA S     10,=F'2'
         MVI   0(10),C'.'
         BAL   7,IMPRIMIR
         L     8,4(8)
         B     OTRALINE
NADAMOST BAL   7,POP
         LR    7,6
         BR    7


STACK    DS    100FL1
HEAP     DS    1000FL1

VECEST   DC    100XL1'FF'

* NODO DEL VECTOR DE ESTACIONES
NODOVEC  DS    0CL5
FLAG     DS    C
DIRLIST  DS    CL4

* NODO DE LISTA DE CONEXIONES DE UNA ESTACION
NODLIST  DS    0CL5
ESTACION DS    C
DIRSIG   DS    CL4


* PUNTERO A LISTA DE LINEAS
LINEAS   DC    FL1'-1'

DOBLE    DS    D
EMPAQ    DS    PL2
ZONEADO  DS    ZL2

WTOBLOCK DC    H'84'
         DC    H'0'
TEXTO    DC    80CL1' '
VACIO    DC    80CL1' '

AUX      DS    CL4

         END

